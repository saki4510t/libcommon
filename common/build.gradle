plugins {
	id 'com.android.library'
	id 'org.jetbrains.kotlin.android'
	id 'org.jetbrains.kotlin.kapt'
	id 'org.jetbrains.dokka'
	id 'maven-publish'
}

/*
 * libcommon
 * utility/helper classes for myself
 *
 * Copyright (c) 2014-2025 saki t_saki@serenegiant.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
*/

android {
	namespace 'com.serenegiant.common'
    compileSdk versionCompiler

   	compileOptions {
   		sourceCompatibility javaSourceCompatibility
   		targetCompatibility javaTargetCompatibility
   	}

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion versionTarget
		// AGP 4.1.0以降でライブラリプロジェクトのBuildConfigにVERSION_CODEとVERSION_NAMEが
		// 含まれなくなってしまったのでワークアラウンドを追加
		buildConfigField 'int', 'VERSION_CODE', "${rootProject.versionCodeNum}"
		buildConfigField 'String', 'VERSION_NAME', "\"${rootProject.versionNameString}\""

		vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
		debug {
			multiDexEnabled true
		}
	}

	buildFeatures {
	    buildConfig = true
		dataBinding = true
	}

	kotlinOptions.jvmTarget = "17"
}

dependencies {
	// androidx
	implementation "androidx.legacy:legacy-support-v4:$androidXLegacyV4"
	implementation "androidx.appcompat:appcompat:$androidXAppCompat"
	//noinspection KtxExtensionAvailable
	implementation "androidx.preference:preference:$androidXPreference"
	implementation "androidx.recyclerview:recyclerview:$androidXRecyclerview"
	implementation "androidx.exifinterface:exifinterface:$androidXExif"
	implementation "com.google.android.material:material:$androidXMaterial"
	implementation "androidx.lifecycle:lifecycle-process:$androidXLifecycle"
	implementation "androidx.databinding:databinding-common:$androidXDataBinding"
	// ktx
	implementation "androidx.core:core-ktx:$androidXCoreKtx"
	implementation "androidx.preference:preference-ktx:$androidXPreference"
	implementation "androidx.fragment:fragment-ktx:$androidXFragmentKtx"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifecycleKtx"
	implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidXLifecycleKtx"
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidXLifecycleKtx"
	implementation "androidx.lifecycle:lifecycle-service:$androidXLifecycleKtx"
	implementation "androidx.lifecycle:lifecycle-common-java8:$androidXLifecycleKtx"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

//	implementation project(':common:core')
//	implementation project(':common:gl')
//	implementation project(':common:media')
}

def repo = new File(rootDir, "repository")

//apply plugin: 'maven-publish'

afterEvaluate {
	publishing {
		repositories {
			maven {
				url = "file://${repo.absolutePath}"
			}
		}
		publications {
			release(MavenPublication) {
				groupId = 'com.serenegiant'
				artifactId = 'common'
				version = versionNameString
				from components.release
				pom {
					inceptionYear = '2014'
					licenses {
						license {
							name = 'The Apache Software License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'saki4510t'
							name = 't_saki'
							email = 't_saki@serenegiant.com'
						}
					}
				}
			}
		}
	}
}
