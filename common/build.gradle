plugins {
	id 'com.android.library'
	id 'kotlin-android'
	id 'kotlin-kapt'
}

/*
 * libcommon
 * utility/helper classes for myself
 *
 * Copyright (c) 2014-2022 saki t_saki@serenegiant.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
*/

android {
    compileSdkVersion versionCompiler
//	buildToolsVersion versionBuildTool

   	compileOptions {
   		sourceCompatibility javaSourceCompatibility
   		targetCompatibility javaTargetCompatibility
   	}

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion versionTarget
        versionCode versionCodeNum
        versionName versionNameString
		// AGP 4.1.0以降でrootプロジェクト以外のBuildConfigにVERSION_CODEとVERSION_NAMEが
		// 含まれなくなってしまったのでワークアラウンドを追加
		buildConfigField 'int', 'VERSION_CODE', "${rootProject.versionCodeNum}"
		buildConfigField 'String', 'VERSION_NAME', "\"${rootProject.versionNameString}\""

        buildConfigField "String", "STL_NAME", "\"${project.property("STL_NAME")}\""

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		vectorDrawables.useSupportLibrary = true
		multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

	buildFeatures {
	    buildConfig = true
	}

	kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

	testImplementation 'junit:junit:4.13.2'

	// Core library
	androidTestImplementation 'androidx.test:core:1.4.0'
	// AndroidJUnitRunner and JUnit Rules
	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	// Assertions
	androidTestImplementation 'androidx.test.ext:junit:1.1.3'
//	androidTestImplementation 'androidx.test.ext:truth:1.2.0'
//	androidTestImplementation 'com.google.truth:truth:0.42'

	// Espresso dependencies
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
//	androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
//	androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
//	androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
//	androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
//	androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
//	androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
	androidTestImplementation "androidx.multidex:multidex:${androidXMultiDex}"

	//noinspection GradleDependency
	implementation "androidx.core:core-ktx:${androidXCore}"
	implementation "androidx.legacy:legacy-support-v4:${androidXLegacyV4}"
	//noinspection GradleDependency
	implementation "androidx.appcompat:appcompat:${androidXVersion}"
	implementation "androidx.recyclerview:recyclerview:${androidXRecyclerview}"
	implementation "androidx.preference:preference-ktx:${androidXPreference}"
	implementation "androidx.lifecycle:lifecycle-extensions:${androidXLifecycleExt}"
	implementation "androidx.exifinterface:exifinterface:${androidXExif}"
	implementation "com.google.android.material:material:${androidXMaterial}"

	//noinspection GradleDependency
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${androidXLifecycleKtx}"
	//noinspection GradleDependency
	kapt "androidx.lifecycle:lifecycle-common-java8:$androidXLifecycleKtx"
}

repositories {
	mavenCentral()
}

def repo = new File(rootDir, "repository")

apply plugin: 'maven-publish'

afterEvaluate {
	publishing {
		repositories {
			maven {
				url = "file://${repo.absolutePath}"
			}
		}
		publications {
			release(MavenPublication) {
				groupId = 'com.serenegiant'
				artifactId = 'common'
				version = versionNameString
				from components.release
				pom {
					inceptionYear = '2014'
					licenses {
						license {
							name = 'The Apache Software License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'saki4510t'
							name = 't_saki'
							email = 't_saki@serenegiant.com'
						}
					}
				}
			}
		}
	}
}
